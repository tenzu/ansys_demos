FINISH
/CLEAR,NOSTART

/NOPR
KEYW,PR_SET,1
KEYW,PR_STRUC,1
KEYW,LSDYNA,1
KEYW,PR_DYNA,1
!/CWD,'C:\Users\Cliff\Desktop\tmp\2D_pullout'

PI = acos(-1)
r = 1	!spiral fibre coil radius (mm)
s = 10 	!spiral fibre pitch (mm)
n = 32	!division in single pitch (n must be even multiple of 4)
p = 1+16/n	!spital fibre total pitch number (must be an integer plus integral multiple of 4/n)
K_num = p*n+1	!total node number in one spiral fibre
L_num = K_num-1 !total element number in one spiral fibre
r_beam = 0.25   !radius of fibre section (mm)
r_margin = r_beam/2.00    !gap between fibre and concrete
r1 = 2*r    !radius of inner cylindar
!H = 75  !total depth of specimen (mm)
H = 1.50*s*p  !total depth of specimen (mm)
y_velo = 10 !pull velocity (m/s)

/PREP7
ET,1,SHELL163
KEYOPT,1,1,2
KEYOPT,1,2,0
KEYOPT,1,3,0
KEYOPT,1,4,0,
ET,2,BEAM161
KEYOPT,2,1,1
KEYOPT,2,2,2
KEYOPT,2,4,0,
KEYOPT,2,5,1
*SET,_RC_SET,1,
R,1
RMODIF,1,1, , ,1,1,1,1,
*SET,_RC_SET,2,
R,2
RMODIF,2,1,1,2.0*r_beam,2.0*r_beam,0,0,
RMODIF,2,6,0,0
MPTEMP,,,,,,,   !concrete material model
MP,DENS,1,0.0024
MP,EX,1,2e4
MP,NUXY,1,0.2
MPTEMP,,,,,,,   !bonding material model
MP,DENS,2,0.0022
MP,EX,2,3e4
MP,NUXY,2,0.2
MPTEMP,,,,,,,   !fibre material model
MP,DENS,3,0.0078
MP,EX,3,2e5
MP,NUXY,3,0.28

*DIM,SPLINE,ARRAY,n*p+1,2,1, , ,
*DIM,K_spline,ARRAY,n*p+1,1,1, , ,
*DO,i,0,n*p,1   !keypoints for spline
    *SET,SPLINE(i+1,1,1),(s/n)*i
    *SET,SPLINE(i+1,2,1),r*sin(2*PI*i/n)
    K,,SPLINE(i+1,1,1),SPLINE(i+1,2,1)
    *GET,K_max,KP,0,NUM,MAX
    *SET,K_spline(i+1,1,1),K_max
*ENDDO
FLST,3,K_max,3
*DO,i,1,K_max,1
	FITEM,3,i
*ENDDO
BSPLIN, ,P51X	!draw spline
LGEN,2,1, , , ,r_margin, , ,0
LGEN,2,1, , , ,-r_margin, , ,0
LSTR,1,K_max+1
LSTR,1,K_max+3
LSTR,K_max,K_max+2
LSTR,K_max,K_max+4
AL,1,4,2,6  !create spline area, fixed line number!!!
AL,1,5,3,7  !create spline area, fixed line number!!!
RECTNG,0,s*p,-r1,r1,  !draw concrete
RECTNG,s*p,H,-r1,r1,
RECTNG,0,s*p,r1,H/2,
RECTNG,s*p,H,r1,H/2,
RECTNG,0,s*p,-r1,-H/2,
RECTNG,s*p,H,-r1,-H/2,
AOVLAP,ALL
NUMCMP,ALL

LSEL,S,LOC,X,0, !mesh sizes for lines
LSEL,A,LOC,X,s*p,
LSEL,A,LOC,X,H,
LSEL,R,LOC,Y,-r1,r1,
LESIZE,ALL,r_margin, , , , , , ,0,
ALLSEL,ALL
LSEL,S,LOC,X,0.25*s*p,0.75*s*p
!LESIZE,ALL,r_margin, , , , , , ,1,
LESIZE,ALL, , ,n*p, ,1, , ,0, 
ALLSEL,ALL
LSEL,S,LOC,X,s*p+0.25*(H-s*p),s*p+0.75*(H-s*p)
LESIZE,ALL, , ,1.00*n*(H-s*p)/s, , , , ,0,
ALLSEL,ALL
LSEL,S,LOC,Y,r1+0.25*(H-r1)/2,r1+0.75*(H-r1)/2
LSEL,A,LOC,Y,-r1-0.25*(H-r1)/2,-r1-0.75*(H-r1)/2
LESIZE,ALL,1.00*s/n , , , , , , ,0,
ALLSEL,ALL

AATT,2,1,1,0    !mesh attribute for bond matireal
ASEL,S, , ,1    !!!fixed area number!!!
ASEL,A, , ,2    !!!fixed area number!!!
MSHKEY,1    !area meshing
AMESH,ALL
ALLSEL,ALL
AATT,1,1,1,0    !mesh attribute for concrete
ASEL,U, , ,1    !!!fixed area number!!!
ASEL,U, , ,2    !!!fixed area number!!!
FLST,2,4,4,ORDE,4   
FITEM,2,14      !!!fixed line number!!!
FITEM,2,6       !!!fixed line number!!!
FITEM,2,7       !!!fixed line number!!!
FITEM,2,16      !!!fixed line number!!!
LCCAT,P51X
MSHKEY,1    !area meshing
AMESH,ALL
ALLSEL,ALL

LSEL,S, , ,1    !!!fixed line number!!!
NSLL,S,1
*GET,N_NUM,NODE,0,COUNT
tmp_N_NUM = 100000
NUMSTR,ELEM,tmp_N_NUM
N,tmp_N_NUM,-10.0*r_margin,0,0
CM,fibre_node_CM,NODE
CMSEL,A,fibre_node_CM
TYPE,2
MAT,3
REAL,2
ESYS,0
SECNUM,

NSEL,S, , ,tmp_N_NUM    !create beam element
NSEL,A, , ,1
*GET,x1,NODE,tmp_N_NUM,LOC,X    !!!fixed node numbers below!!!
*GET,y1,NODE,tmp_N_NUM,LOC,Y
*GET,x2,NODE,1,LOC,X
*GET,y2,NODE,1,LOC,Y
N, ,x1+abs(x2-x1)/2.0,0.5,0
E,tmp_N_NUM,1,tmp_N_NUM+1
*GET,tmp_N_NUM,NODE,0,NUM,MAX
NSEL,S, , ,1
NSEL,A, , ,3
*GET,x1,NODE,1,LOC,X
*GET,y1,NODE,1,LOC,Y
*GET,x2,NODE,3,LOC,X
*GET,y2,NODE,3,LOC,Y
N, ,x1+abs(x2-x1)/3.0,((x1**2-2*x1*x2+x2**2-y1**2)+(y2**2))/2/(y2-y1),0
E,1,3,tmp_N_NUM+1
*GET,tmp_N_NUM,NODE,0,NUM,MAX
*DO,i,3,N_NUM-1,1
    *GET,x1,NODE,i,LOC,X
    *GET,y1,NODE,i,LOC,Y
    *GET,x2,NODE,i+1,LOC,X
    *GET,y2,NODE,i+1,LOC,Y
    NSEL,S, , ,i
    NSEL,S, , ,i+1
    N, ,x1+abs(x2-x1)/3.0,((x1**2-2*x1*x2+x2**2-y1**2)+(y2**2))/2/(y2-y1),0
    E,i,i+1,tmp_N_NUM+1
    *GET,tmp_N_NUM,NODE,0,NUM,MAX
*ENDDO
ALLSEL,ALL

EDPART,CREATE   !create all parts
!!!EDCGEN,TSTS,1,2,0.15,0.1,0,0,0.2,100,3.5, , ,0,90000000,0,0   
!!!EDCGEN,TSTS,2,3,0.15,0.1,0,0,0.2,100,3.5, , ,0,90000000,0,0
!EDCGEN,ASTS,1,3,0.15,0.1,0,0,0.2, , , , ,0,90000000,0,0 

NSEL,S, , ,100000   !PULL for pull
CM,PULL,NODE
CMSEL,A,PULL
*DIM,TIME,ARRAY,3,1,1, , ,
*SET,TIME(2,1,1) , 1E3
*SET,TIME(3,1,1) , 1E9
*DIM,VELO,ARRAY,3,1,1, , ,
*SET,VELO(1,1,1) ,-y_velo/6E4
*SET,VELO(2,1,1) ,-y_velo/6E4
*SET,VELO(3,1,1) ,-y_velo/6E4
EDLOAD,ADD,VX,0,PULL,TIME,VELO, 0, , , , ,
ALLSEL,ALL

NSEL,S,LOC,X,H  !fix end
CM,FIX,NODE
CMSEL,A,FIX
D,ALL, , , , , ,ALL, , , , ,   
ALLSEL,ALL
NUMMRG,ALL, , , ,LOW
NUMCMP,ALL  

FINISH
/SOL
TIME,3,
EDRST, ,0.1,
EDHTIME, ,0,
EDDUMP, ,0,
EDWRITE,LSDYNA,'tmp','k',' '